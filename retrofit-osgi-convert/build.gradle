buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'org.standardout:bnd-platform:1.2.0'
	}
}

apply plugin: 'org.standardout.bnd-platform'

repositories {
	// mavenLocal()
	mavenCentral()
}

platform {

	// Uncomment with proper path to your eclipse installation (necessary for building a p2 updatesite)
	// eclipseHome = new File('/home/simon/eclipse/eclipse_luna_modeling')

	// configure proper names for the p2 update site
	categoryId = 'retrofit'
	categoryName = 'retrofit'

	featureId = 'retrofit'
	featureName = 'retrofit osgi'

	// Convert the retrofit artifact to OSGi, make android.os optional and handle the split package problems in OSGi
	bundle('com.squareup.retrofit:retrofit-simon:2.0.0-SNAPSHOT'){
		bnd {
			optionalImport 'android.os'
			instruction 'Export-Package', 'retrofit;com.squareup.retrofit=split;mandatory:=com.squareup.retrofit, retrofit.http'
	    	}
	}

	// Convert the retrofit gson converter artifact to OSGi and handle the split package problems in OSGi
	bundle('com.squareup.retrofit:converter-simon-gson:2.0.0-SNAPSHOT') {
		bnd{
			instruction 'Require-Bundle', 'com.squareup.retrofit.simon'
			instruction 'Export-Package', 'retrofit;com.squareup.retrofit.converter-gson=split;mandatory:=com.squareup.retrofit.converter-gson'
		}
	}

	// You can add other converters similar to the gson converter above...
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.8'
}
